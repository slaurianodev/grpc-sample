buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

plugins {
  id "com.google.protobuf" version "0.8.3"
  id "java"
  id "application"
  id "idea"
  id 'com.github.ben-manes.versions' version '0.17.0'
}

group 'br.com.sergio.grpc'
version '1.0.0'

protobuf {
    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'io.grpc:grpc-all:1.9.0'
    compile  'org.hibernate:hibernate-core:4.3.6.Final'
    compile 'org.slf4j:slf4j-simple:1.7.7'
//    compile 'mysql:mysql-connector-java:5.1.31'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
    }
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'br.com.sergio.wallet.WalletServer'
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'br.com.sergio.wallet.WalletClient'
}

startScripts.enabled = false

task walletServer(type: CreateStartScripts) {
    mainClassName = "br.com.sergio.wallet.WalletServer"
    applicationName = "wallet-server"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task walletClient(type: CreateStartScripts) {
    mainClassName = "br.com.sergio.wallet.WalletClient"
    applicationName = "wallet-client"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(walletServer)
    from(walletClient)
    fileMode = 0755
}
